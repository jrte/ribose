#May 15 07:58:52 kb-ubuntu kernel: [ 1794.599801] DROPPED IN=eth0 OUT= MAC=01:00:5e:00:00:fb:00:13:20:c0:36:32:08:00 SRC=192.168.144.101 DST=224.0.0.251 LEN=32 TOS=0x00 PREC=0x00 TTL=1 ID=8596 OPT (94040000) PROTO=2 

datetime = upper lower+ space digit+ space (digit+ ':')+ digit+;

maybe = {alpha, digit, punct}*;
value = {alpha, digit, punct}+;

header = (
	(nil, select[`~timestamp`]) (datetime @@ PasteAny)
	(space,select[`~hostname`]) (maybe @@ PasteAny) 
	(space 'kernel: [' (space* digit+ '.' digit+) ']')
	(space)
);

capture = (
	(
		(space)
		(
			(
				('IN=', select[`~in`])
			|	('OUT=', select[`~out`])
			|	('MAC=', select[`~macaddress`])
			|	('SRC=', select[`~srcip`])
			|	('DST=', select[`~dstip`])
			|	('SPT=', select[`~srcport`])
			|	('DPT=', select[`~dstport`]) 
			|	('PROTO=', select[`~protocol`])
			)
			(maybe @@ PasteAny)
		||	maybe
		)
	)+
	(space)
);

nev = (
	out[`~` ` ` `~timestamp` ` ` `~hostname` ` ` `~in` ` ` `~out` ` ` `~macaddress` ` ` `~srcip` ` ` `~dstip` ` ` `~protocol` ` ` `~srcport` ` ` `~dstport` `\n`] 
);

LinuxKernelDropped = (
	header 'DROPPED' capture (nl, select paste[`DROPPED`] nev in[`!nil`] stop)
);

LinuxKernelLimited = (
	header 'LIMITED' capture (nl, select paste[`LIMITED`] nev in[`!nil`] stop)
);

LinuxKernelAborted = (
	header 'ABORTED' capture  (nl, select paste[`ABORTED`] nev in[`!nil`] stop)
);

LinuxKernel = (
	(
		(nil, clear[`~*`] mark)
		(
			(
				(nil \ (LinuxKernelDropped$0), reset in[`!nil`] start[`@LinuxKernelDropped`])
			|	(nil \ (LinuxKernelLimited$0), reset in[`!nil`] start[`@LinuxKernelLimited`])
			|	(nil \ (LinuxKernelAborted$0), reset in[`!nil`] start[`@LinuxKernelAborted`])
			)
		||	(any - nl)* (nl, in[`!nil`])
		)
	)*
);

LinuxKernelDropped :save `LinuxKernelDropped.dfa`;
LinuxKernelLimited :save `LinuxKernelLimited.dfa`;
LinuxKernelAborted :save `LinuxKernelAborted.dfa`;
LinuxKernel :save `LinuxKernel.dfa`;
