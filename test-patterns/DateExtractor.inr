delim = {'-','/','.','\\'};
white = {space, tab, comma, nl, eos};
yyyy = ((digit-'0') (digit:3)) @@ PasteAny;
md = ('0'? (digit-'0') | '1' ('0'|'1'|'2')) @@ PasteAny;
dd = ('1' (digit-('0'|'1'|'2')) | '2' digit | '3' ('0'|'1')) @@ PasteAny;

qy = `!_dmy`;
yq = `!_ydm`;

cutd0 = select[`~d0`] cut[`~`] select[`~`];
cutd1 = select[`~d1`] cut[`~`] select[`~`];
cutd2 = select[`~d2`] cut[`~`] select[`~`];

numericDate = (
	(((digit$(0,0))* (delim, '-')*)* @@ (digit+ '-' digit+ '-' digit+)) (white$(0,0))
@	(	md ('-', cutd0) dd ('-', cutd1) yyyy (white, cutd2 in[`!_mdy`])
	|	dd ('-', cutd0) md ('-', cutd1) yyyy (white, cutd2 in[`!_dmy`])
	|	yyyy ('-', cutd0) md ('-', cutd1) dd (white, cutd2 in[`!_ymd`])
	|	yyyy ('-', cutd0) dd ('-', cutd1) md (white, cutd2 in[`!_ydm`])
	|	md ('-', cutd0) md ('-', cutd1) yyyy (white, cutd2 in[qy])
	|	yyyy ('-', cutd0) md ('-', cutd1) md (white, cutd2 in[yq])
	)
);

numericDate$(0,1 2):sseq:report;

day = (dd | md) {'st','nd','rd','th'}?;

month = {
	({'J','j'} 'an', select[`~?`] paste[`01`] select[`~`]) 'uary'?,
	({'F','f'} 'eb', select[`~?`] paste[`02`] select[`~`]) 'ruary'?,
	({'M','m'} 'ar', select[`~?`] paste[`03`] select[`~`]) 'ch'?,
	({'A','a'} 'pr', select[`~?`] paste[`04`] select[`~`]) 'il'?,
	({'M','m'} 'ay', select[`~?`] paste[`05`] select[`~`]),
	({'J','j'} 'un', select[`~?`] paste[`06`] select[`~`]) 'e'?,
	({'J','j'} 'ul', select[`~?`] paste[`07`] select[`~`]) 'y'?,
	({'A','a'} 'ug', select[`~?`] paste[`08`] select[`~`]) 'ust'?,
	({'S','s'} 'ep', select[`~?`] paste[`09`] select[`~`]) 'tember'?,
	({'O','o'} 'ct', select[`~?`] paste[`10`] select[`~`]) 'ober'?,
	({'N','n'} 'ov', select[`~?`] paste[`11`] select[`~`]) 'ember'?,
	({'D','d'} 'ec', select[`~?`] paste[`12`] select[`~`]) 'ember'?
};

monthday = month @ ((((month$2:alph) - `~?`)$(0,0))* (`~?`, `~d0`)*)*;
daymonth = month @ ((((month$2:alph) - `~?`)$(0,0))* (`~?`, `~d1`)*)*;

alphanumericDate = (
	day (white, select[`~d0`] cut[`~`]) white* 'of'? white* daymonth white+ yyyy (white, select[`~d2`] cut[`~`] select[`~`] in[`!_dmy`])
| 	monthday white+ day (white, select[`~d1`] cut[`~`] select[`~`]) white* yyyy (white, select[`~d2`] cut[`~`] select[`~`] in[`!_mdy`])
);

alphanumericDate$(0,1 2):sseq:report;

date = (numericDate | alphanumericDate);

date$(0,1 2):sseq:report;

noise = (utf7 - white) - (utf7 & (date$0:pref));

a0 = (date$0):alph;
a1 = (date$1):alph;
a2 = (date$2):alph;

notdate = (
	((AnyOrNul* @ date)$(0 1 2))
@	((a0$(0,0))* (a1$(0,,0))* (a2$(0,,,0))*)*
	(nul, nul, retry in[`~`] clear[`~*`] select)
	(nul* a0* a1* a2*)* 
);

notdate$(0,1 2):sseq:report;

DateExtractor = (
	nil?
	(
		white* noise* notdate*
		(
			date
			(
				(`_mdy`, out['_mdy ' `~d2` '=' `~d0` '=' `~d1` nl] clear[`~*`] select)
			|	(`_dmy`, out['_dmy ' `~d2` '=' `~d1` '=' `~d0` nl] clear[`~*`] select)
			|	(`_ymd`, out['_ymd ' `~d0` '=' `~d1` '=' `~d2` nl] clear[`~*`] select)
			|	(`_ydm`, out['_ydm ' `~d0` '=' `~d2` '=' `~d1` nl] clear[`~*`] select)
			)
		)*
	)*
);

DateExtractor:dfamin:pr `DateExtractor.pr`;
DateExtractor$(0,1 2):dfamin:prsseq `DateExtractor.sseq`;
DateExtractor:dfamin:save `DateExtractor.dfa`;

