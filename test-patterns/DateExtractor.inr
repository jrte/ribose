delim = ({'-','/','.','\\'}, '-');
white = {space, tab, comma, nl, eos};
yyyy = ((digit-'0') (digit:3)) @@ PasteAny;
md = ('0'? (digit-'0') | '1' ('0'|'1'|'2')) @@ PasteAny;
dd = ('1' (digit-('0'|'1'|'2')) | '2' digit | '3' ('0'|'1')) @@ PasteAny;

qy = `!_dmy`;
yq = `!_ydm`;

d0 = ('-', select[`~d0`] cut[`~`] select[`~`]);
d1 = ('-', select[`~d1`] cut[`~`] select[`~`]);
d2 = select[`~d2`] cut[`~`] select[`~`];

numericDate = (
	(((digit$(0,0))* delim*)* @@ (digit+ '-' digit+ '-' digit+)) (white$(0,0))
@	(	md d0	dd d1 yyyy	(white, d2 in[`!_mdy`])
	|	dd d0	md d1 yyyy	(white, d2 in[`!_dmy`])
	|	yyyy d0	md d1 dd	(white, d2 in[`!_ymd`])
	|	yyyy d0	dd d1 md	(white, d2 in[`!_ydm`])
	|	md d0	md d1 yyyy	(white, d2 in[qy])
	|	yyyy d0	md d1 md	(white, d2 in[yq])
	)
);

numericDate$(0,1 2):sseq:report;

day = (dd | md) {'st','nd','rd','th'}?;

month = {
	({'J','j'} 'an', select[`~?`] paste[`01`] select[`~`]) 'uary'?,
	({'F','f'} 'eb', select[`~?`] paste[`02`] select[`~`]) 'ruary'?,
	({'M','m'} 'ar', select[`~?`] paste[`03`] select[`~`]) 'ch'?,
	({'A','a'} 'pr', select[`~?`] paste[`04`] select[`~`]) 'il'?,
	({'M','m'} 'ay', select[`~?`] paste[`05`] select[`~`]),
	({'J','j'} 'un', select[`~?`] paste[`06`] select[`~`]) 'e'?,
	({'J','j'} 'ul', select[`~?`] paste[`07`] select[`~`]) 'y'?,
	({'A','a'} 'ug', select[`~?`] paste[`08`] select[`~`]) 'ust'?,
	({'S','s'} 'ep', select[`~?`] paste[`09`] select[`~`]) 'tember'?,
	({'O','o'} 'ct', select[`~?`] paste[`10`] select[`~`]) 'ober'?,
	({'N','n'} 'ov', select[`~?`] paste[`11`] select[`~`]) 'ember'?,
	({'D','d'} 'ec', select[`~?`] paste[`12`] select[`~`]) 'ember'?
};

m0 = ((((month$2:alph) - `~?`)$(0,0))* (`~?`, `~d0`)*)*;
m1 = ((((month$2:alph) - `~?`)$(0,0))* (`~?`, `~d1`)*)*;

alphanumericDate = (
	day (white, select[`~d0`] cut[`~`]) white* 'of'? white* (month @ m1) white+ yyyy (white, select[`~d2`] cut[`~`] select[`~`] in[`!_dmy`])
| 	(month @ m0) white+ day (white, select[`~d1`] cut[`~`] select[`~`]) white* yyyy (white, select[`~d2`] cut[`~`] select[`~`] in[`!_mdy`])
);

alphanumericDate$(0,1 2):sseq:report;

date = (numericDate | alphanumericDate);

date$(0,1 2):sseq:report;

noise = (utf7 - white) - (utf7 & (date$0:pref));

reset = clear[`~*`] select;

a0 = (date$0):alph;
a1 = (date$1):alph;
a2 = (date$2):alph;

notdate = (
	((AnyOrNul* @ date)$(0 1 2))
@	((a0$(0,0))* (a1$(0,,0))* (a2$(0,,,0))*)*
	(nul, nul, retry in[`~`] reset)
	(nul* a0* a1* a2*)* 
);

notdate$(0,1 2):sseq:report;

mdy = out[`~d2` '/' `~d0` '/' `~d1` nl] reset;
dmy = out[`~d2` '/' `~d1` '/' `~d0` nl] reset;
ymd = out[`~d0` '/' `~d1` '/' `~d2` nl] reset;
ydm = out[`~d0` '/' `~d2` '/' `~d1` nl] reset;

DateExtractor = (
	nil?
	(
		white* noise* notdate*
		(
			date
			(
				(`_mdy`, mdy)
			|	(`_dmy`, dmy)
			|	(`_ymd`, ymd)
			|	(`_ydm`, ydm)
			)
		)*
	)*
);

DateExtractor:dfamin:pr `DateExtractor.pr`;
DateExtractor$(0,1 2):dfamin:prsseq `DateExtractor.sseq`;
DateExtractor:dfamin:save `DateExtractor.dfa`;

