# <cycle-start id="4" type="scavenge" contextid="0" timestamp="2021-04-05T23:12:58.597" intervalms="5243.468" />
next = (byte - '<')*;

interval = (
	'<cycle-start id="' digit+ '" type="scavenge" contextid="' digit+ '" timestamp="' (digit+ ('.:-T':alph))+ digit+ '" '
	('intervalms="', select[`~interval`] clear)
	(digit, paste)+ ('.', paste) (digit, paste)+
	('"', out[`~interval` X_]) space* '/>'
	next
);

a0 = (interval$0):alph;
a1 = (interval$1):alph;
a2 = (interval$2):alph;

(`*** Tintervals: tape alphabets are not disjoint`
	(a0 & a1) | (a0 & a2) | (a1 & a2)
):enum;

void = (next '<' (byte - 'c'))* next;

null = (
	(
		((AnyOrNul* @ interval)$(0 1 2))
	@	((a0$(0,0))* (a1$(0,,0))* (a2$(0,,,0))*)*
		(nul$(0,0)) (nul* a0* a1* a2*)* 
	)
	void
);

Tintervals = nil? (
	((null* interval*)*$(0 1 2))
@	(
		({a0,nul}$(0,0))* (a1$(0,,0))*
		((a2-X_)$(0,,,0))*
		(X_, [[`nl`]])* 
	)*
):dfamin;

Tintervals$(0,1 2):prsseq `build/automata/Tintervals.pr`;
Tintervals:save `build/automata/Tintervals.dfa`;

interval = (
	'<cycle-start id="' digit+ '" type="scavenge"'
	(
		space+
		(
				('intervalms="', select clear)
				(digit, paste)+ ('.', paste) (digit, paste)+
				('"', out[X_ Y_])
		||	((lower* '="') - 'intervalms="')
				(byte - '"')* '"'
		)
	)*
	space* '/>' next
)*;

a0 = (interval$0):alph;
a1 = (interval$1):alph;
a2 = (interval$2):alph;

(`*** Sintervals: tape alphabets are not disjoint`
	(a0 & a1) | (a0 & a2) | (a1 & a2)
):enum;

null = (
	(
		((AnyOrNul* @ interval)$(0 1 2))
	@	((a0$(0,0))* (a1$(0,,0))* (a2$(0,,,0))*)*
		(nul$(0,0)) (nul* a0* a1* a2*)* 
	)
	void
);

intervals = (
	((interval* null*)*$(0 1 2))
@	(
		({a0,nul}$(0,0))* (a1$(0,,0))* ((a2-{X_,Y_})$(0,,,0))*
		(X_, [[`~`]])* (Y_, [[`nl`]])*
	)*
);

Sintervals = (nil? intervals):dfamin;

Sintervals$(0,1 2):prsseq `build/automata/Sintervals.pr`;
Sintervals:save `build/automata/Sintervals.dfa`;

opennil = {
	('<' {'?','!','/'}),
	('<af-end'),
	('<af-start'),
	('<allocated-bytes'),
	('<allocation-satisfied'),
	('<allocation-stats'),
	('<attribute'),
	('<cycle-end'),
	('<exclusive-end'),
	('<exclusive-start'),
	('<finalization'),
	('<gc-end'),
	('<gc-op'),
	('<gc-start'),
	('<initialized'),
	('<largest-consumer'),
	('<mem'),
	('<mem-info'),
	('<memory-copied'),
	('<ownableSynchronizers'),
	('<pending-finalizers'),
	('<references'),
	('<remembered-set'),
	('<response-info'),
	('<scavenger-info'),
	('<system'),
	('<verbosegc'),
	('<vmarg'),
	('<vmargs')
};

interval = (
	((interval | null)$(0 1 2))
@	(
		({a0,nul}$(0,0))* (a1$(0,,0))* ((a2-{X_,Y_})$(0,,,0))*
		(X_, [[`~`]])* (Y_, [[`nl`]])*
	)*
);

Rintervals = (
	nil? (next (opennil | interval))* next
):dfamin;

Rintervals$(0,1 2):prsseq `build/automata/Rintervals.pr`;
Rintervals:save `build/automata/Rintervals.dfa`;
